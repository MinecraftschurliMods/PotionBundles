//file:noinspection GroovyAssignabilityCheck
import groovy.json.JsonSlurper
import groovy.json.JsonOutput

buildscript {
    repositories {
        maven { url = 'https://files.minecraftforge.net/maven' }
        maven { url = 'https://maven.parchmentmc.org' }
        mavenCentral()
    }
    dependencies {
        classpath group: 'net.minecraftforge.gradle', name: 'ForgeGradle', version: "${project.forgegradle_version}", changing: true
        classpath group: 'org.parchmentmc', name: 'librarian', version: "${project.librarian_version}"
    }
}

plugins {
    id 'maven-publish'
    id 'eclipse'
    id 'idea'
}

apply plugin: 'net.minecraftforge.gradle'
apply plugin: 'org.parchmentmc.librarian.forgegradle'
//--------------

group = "${project.mod_group}"
version = "${project.mc_version}-${project.mod_version}"
archivesBaseName = "${project.mod_id}"

if (System.getenv('RELEASE_TYPE') != null) {
    status = System.getenv('RELEASE_TYPE').toLowerCase()
    if (status == 'snapshot') status = status.toUpperCase()
    version = "${version}-${status}"
}

java {
    withSourcesJar()

    toolchain {
        languageVersion = JavaLanguageVersion.of(project.java_version)
        vendor = JvmVendorSpec.ADOPTOPENJDK
    }
}

tasks.withType(JavaCompile) {
    options.encoding = 'UTF-8'
}

sourceSets {
    main {
        resources {
            srcDir 'src/main/generated'
        }
    }
}

repositories {
    mavenLocal()
    mavenCentral()
    maven {
        name = 'Minecraftschurli Maven'
        url = 'https://minecraftschurli.ddns.net/repository'
    }
    maven {
        name = "Progwml6 maven"
        url = "https://dvs1.progwml6.com/files/maven/"
    }
    maven {
        name = "ModMaven"
        url = "https://modmaven.k-4u.nl"
    }
}

dependencies {
    minecraft "net.minecraftforge:forge:${project.mc_version}-${project.forge_version}"

    // JEI
    compileOnly fg.deobf("mezz.jei:jei-1.18:${project.jei_version}:api")
    runtimeOnly fg.deobf("mezz.jei:jei-1.18:${project.jei_version}")
}

minecraft {
    mappings channel: "${project.mappings_channel}", version: "${project.mappings_version}"
    runs {
        client {
            workingDirectory project.file('run')
            property 'forge.logging.console.level', 'debug'
            mods {
                potionbundles {
                    source sourceSets.main
                }
            }
        }
        server {
            workingDirectory project.file('run')
            property 'forge.logging.console.level', 'debug'
            mods {
                potionbundles {
                    source sourceSets.main
                }
            }
        }
        data {
            workingDirectory project.file('run')
            property 'forge.logging.console.level', 'debug'
            args '--mod', "${project.mod_id}", '--all', '--output', file('src/main/generated/'), '--existing', file('src/main/resources/')
            mods {
                potionbundles {
                    source sourceSets.main
                }
            }
        }
    }
}

def replaceIn = ['META-INF/mods.toml']
def replacements = [
        'mod_id': mod_id,
        'mod_name': mod_name,
        'mod_version': mod_version,
        'mod_url': mod_url,
        'mod_authors': mod_authors,
        'mod_description': mod_description,
        'mc_version_range': mc_version_range,
        'forge_version_range': forge_version_range,
        'loader_version_range': loader_version_range
]

processResources {
    duplicatesStrategy DuplicatesStrategy.INCLUDE
    from(sourceSets.main.resources.srcDirs) {
        include replaceIn
        expand replacements
    }
    from(sourceSets.main.resources.srcDirs) {
        exclude replaceIn
    }
    doLast {
        fileTree(dir: outputs.files.asPath, include: "**/*.json").each {
            it.text = JsonOutput.toJson(new JsonSlurper().parse(it))
        }
    }
}

jar {
    from sourceSets.main.output
    finalizedBy('reobfJar')
}

sourcesJar {
    from sourceSets.main.allSource
}

tasks.withType(Jar).configureEach {
    String extension = ''
    if (it.archiveClassifier.present) {
        extension = it.archiveClassifier.get()
        if (extension != '') {
            extension = '-' + extension
        }
    }
    manifest {
        mainAttributes([
                'Maven-Artifact': "${project.mod_group}:${project.archivesBaseName}:${project.version}",
                'Specification-Title': archivesBaseName,
                'Specification-Vendor': "${project.vendor}",
                'Specification-Version': '1',
                'Implementation-Title': "${archivesBaseName}${extension}",
                'Implementation-Version': "${project.mod_version}",
                'Implementation-Vendor': "${project.vendor}",
                'Built-On-Java': "${System.getProperty('java.vm.version')} (${System.getProperty('java.vm.vendor')})",
                'Built-On': "${project.mc_version}-${project.forge_version}",
                'Timestamp': new Date().format("yyyy-MM-dd'T'HH:mm:ssZ"),
                'FMLModType': 'MOD'
        ])
    }
}

artifacts {
    archives jar
    archives sourcesJar
}

reobf {
    jar { classpath.from(sourceSets.main.compileClasspath) }
}

task reobf {
    dependsOn reobfJar
}

task setupGithubActions() {
    doLast {
        println "##[set-output name=modid;]${project.mod_id}"
        if(project.hasProperty('curse_project_id')) {
            println "##[set-output name=project_id;]${project.curse_project_id}"
            if(project.hasProperty('curse_title')) {
                println "##[set-output name=curse_title;]${project.curse_title}"
            }
            println "##[set-output name=curse_versions;]${project.curse_versions}"
            if(project.hasProperty('curse_relations')) {
                println "##[set-output name=curse_relations;]${project.curse_relations}"
            }
        }
        println "##[set-output name=version;]${project.version}"
    }
}

idea {
    module {
        for (String exclude in ['run', 'out', 'libs']) {
            excludeDirs += file(exclude)
        }
    }
}

tasks.withType(GenerateModuleMetadata) {
    enabled = false
}

publishing {
    publications.create(project.archivesBaseName+'ToMaven', MavenPublication) {
        groupId = project.group
        artifactId = project.archivesBaseName
        version = project.version
        from components.java
        pom {
            name = project.mod_name
            url = project.mod_url
            packaging = 'jar'
            scm {
                connection = "scm:git:git://github.com/${project.github}.git"
                developerConnection = "scm:git:git@github.com:${project.github}.git"
                url = "https://github.com/${project.github}"
            }
            issueManagement {
                system = 'github'
                url = "https://github.com/${project.github}.git/issues"
            }
            organization {
                name = 'Minecraftschurli Mods'
                url = 'https://github.com/MinecraftschurliMods'
            }
            developers {
                developer {
                    id = 'minecraftschurli'
                    name = 'Minecraftschurli'
                    url = 'https://github.com/Minecraftschurli'
                    email = 'minecraftschurli@gmail.com'
                    organization = 'Minecraftschurli Mods'
                    organizationUrl = 'https://github.com/MinecraftschurliMods'
                    timezone = 'Europe/Vienna'
                }
                developer {
                    id = 'ichhabehunger54'
                    name = 'IchHabeHunger54'
                    url = 'https://github.com/IchHabeHunger54'
                    organization = 'Minecraftschurli Mods'
                    organizationUrl = 'https://github.com/MinecraftschurliMods'
                    timezone = 'Europe/Vienna'
                }
            }
            withXml {
                asNode().dependencies.dependency.each { dep ->
                    if(dep.version.last().value().last().contains('_mapped_')) {
                        assert dep.parent().remove(dep)
                    }
                }
            }
        }
    }
    repositories {
        maven {
            if (
            (System.getenv("MAVEN_USER") != null) &&
                    (System.getenv("MAVEN_PASSWORD") != null) &&
                    (System.getenv("MAVEN_URL") != null)
            ) {
                url System.getenv("MAVEN_URL")
                credentials {
                    username System.getenv("MAVEN_USER")
                    password System.getenv("MAVEN_PASSWORD")
                }
            } else {
                url "$buildDir/repo"
            }
        }
    }
}
